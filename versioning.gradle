ext {
    versionMajor = 1
    versionMinor = 1
    versionPatch = 2
    versionClassifier = null
    isSnapshot = false

    /**
     * Builds an Android version code from the version of the project.
     *
     * I.e. when the application versionName is 3.1.0, the versionCode for a minimum API level 21 APK
     * would be something like 21030100. The first two digits are reserved for the minimum
     * API Level (21 in this case), and the last six digits are for the applicationâ€™s version name (3.1.0).
     *
     * NOTE: The greatest value Google Play allows for versionCode is 2100000000
     * @return
     */
    buildVersionCode = {
        (sdk.minSdk * 1000000) + (versionMajor * 10000) + (versionMinor * 100) + versionPatch
    }

    /**
     * Builds an Android version name from the version of the project.
     * This is designed to handle the -SNAPSHOT
     *
     * I.e. during development the version ends with -SNAPSHOT and the final release is without any suffix.
     * @return
     */
    buildVersionName = {
        def versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        if (versionClassifier == null && isSnapshot) {
            versionClassifier = "SNAPSHOT"
        }

        if (versionClassifier != null) {
            versionName += "-${versionClassifier}"
        }
        versionName
    }
}
